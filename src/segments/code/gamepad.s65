.proc gamepad_poll
	lda var_gamepad                        ; Load current state of var_gamepad to check button press/released
	pha                                    ; Push it on the stack

	; strobe the gamepad to latch current button state
	lda #1
	sta JOYPAD1
	lda #0
	sta JOYPAD1

	ldx #8                                 ; Read 8 bytes from the interface at $4016 (yes bytes not bits the controller returns a whole byte based on the bit of the current button)
loop:
	pha                                    ; Push to the stack the values we are reading
	lda JOYPAD1                            ; Get the value of the current button we're checking
	and #%00000011                         ; Only keep the low two bits (the rest is noise but do we need to mask it?)                   
	cmp #%00000001                         ; Compare the lsb it contains the information of the current button we're checking
	pla                                    ; Load back the values for the rotation
	ror                                    ; rotate carry into var_gamepad variable
	dex                                    ; --x
	bne loop                               ; If x != 0 loop back

	sta var_gamepad                        ; Store the values into var_gamepad
	pla                                    ; Load values of var_gamepad from previous frame
	sta temp_00                            ; temp storage
	eor var_gamepad                        ; old_gamepad XOR var_gamepad
	and temp_00                            ; (old_gamepad XOR var_gamepad) AND old_gamepad
	sta var_gamepad_released                   ; Store the released buttons

	rts                                    ; return
.endproc