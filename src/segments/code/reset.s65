;===================================================================================================
; RESET is the entry point where the program starts, triggered when the system is powered on or reset.
;===================================================================================================

.proc reset
	sei 			; disable interrupts
	lda #0
	sta PPU_CONTROL
	sta PPU_MASK
	sta APU_DM_CONTROL
	lda #$40
	sta JOYPAD2

	; Disable decimal mode
	cld
	ldx #$FF
	txs

	; Wait for V-Blank
	bit PPU_STATUS
wait_vblank:
	bit PPU_STATUS
	bpl wait_vblank

	lda #0
	ldx #0
clear_ram:
	sta $0000, x
	sta $0100, x
	sta $0200, x
	sta $0300, x
	sta $0400, x
	sta $0500, x
	sta $0600, x
	sta $0700, x
	inx
	bne clear_ram

	lda #$FF
	ldx #0
clear_oam:
	sta oam, x
	inx
	inx
	inx
	inx
	bne clear_oam

wait_vblank2:
	bit PPU_STATUS
	bpl wait_vblank2

    ; Copy CHR tiles into CHR RAM
	jsr copy_tiles_chr
	
	jsr write_data_to_chr

    jsr load_bullets                   ; Call load_bullet subroutine to load the bullet into OAM-RAM
    jsr load_player                    ; Call load_player subroutine to load the player into OAM-RAM

	; Enable V-Blank
	lda #%10001000
	sta PPU_CONTROL
init_sound:
    INIT_SOUND_ENGINE
    SELECT_SONG SONG_1
    SELECT_FX FX_1

@init_player_pos:
	ldx #4								; load 4 in X (position of the top left sprite of the player)
	lda player_data, x					; Check player position y in raw data
	sta var_player_y					; Set player position y variable
	inx									;
	inx									; x+3 to go to X position of sprite in the raw data
	inx									;
	lda player_data, x					; Load player position x variable
	sta var_player_x					; Set player position x variable

@init_bools:
	lda var_bools
	ora #%00000011 						; Setting the boolean for firing to true by default bit 0/ Setting fun mode for controls bit 1
	sta var_bools	
	jmp main
.endproc